"
I am an inline block generated by inline splitter, to parse inline text.

I am characterized by:
- start & end: locations of my opener & closer delimiters in initial string
- substring: concerned text (without delimiters so)
- kind: my type, symbol which must be the same than my relative delimiter type (#basic for normal text)
- children: array in case of nested inline blocks


"
Class {
	#name : 'MicInlineElement',
	#superclass : 'MicElement',
	#instVars : [
		'start',
		'end',
		'children',
		'substring'
	],
	#category : 'Microdown-ModelInline',
	#package : 'Microdown',
	#tag : 'ModelInline'
}

{ #category : 'constructor' }
MicInlineElement class >> from: aStartInteger to: anEndInteger withSubstring: aString [
	^ self from: aStartInteger to: anEndInteger withSubstring: aString withChildren: Array new
]

{ #category : 'constructor' }
MicInlineElement class >> from: aStartInteger to: anEndInteger withSubstring: aString withChildren: aChildren [
	^ self new 
		start: aStartInteger; 
		end: anEndInteger; 
		substring: aString; 
		children: aChildren;
		closeMe;  
		cleanSubstring; 
		yourself.	
]

{ #category : 'testing' }
MicInlineElement class >> isEvaluated [
	^ true
]

{ #category : 'accessing' }
MicInlineElement >> children [
	^ children
]

{ #category : 'accessing' }
MicInlineElement >> children: anArray [
	children := anArray
]

{ #category : 'printing' }
MicInlineElement >> childrenPrintOn [
	^ (children asString allButFirst: 9) allButLast
]

{ #category : 'operations' }
MicInlineElement >> cleanSubstring [
	"remove all \ left by the MicInlineParser - and replace \\ with single \"
	self substring: (MicInlineParser escapeDecode: self substring )
]

{ #category : 'visiting' }
MicInlineElement >> closeMe [
	"Hook to perform post creation action. The name is the same as for blocks."
	
]

{ #category : 'accessing' }
MicInlineElement >> end [
	^ end
]

{ #category : 'accessing' }
MicInlineElement >> end: anInteger [
	end := anInteger
]

{ #category : 'operations' }
MicInlineElement >> escapeCharacter [
	^ '\'
]

{ #category : 'initialization' }
MicInlineElement >> initialize [

	super initialize. 
	children := #().
	
]

{ #category : 'testing' }
MicInlineElement >> isOnlyChild [
	^ children isEmpty
]

{ #category : 'accessing' }
MicInlineElement >> kind [
	"we are deprecating the state"
	^ self error
]

{ #category : 'accessing' }
MicInlineElement >> literal [
	^ substring copyFrom: start to: end. 
]

{ #category : 'accessing' }
MicInlineElement >> openingDelimiter [

	self subclassResponsibility
]

{ #category : 'accessing' }
MicInlineElement >> openingDelimiterSize [

	^ self openingDelimiter size
]

{ #category : 'accessing' }
MicInlineElement >> parent: aParent [
	parent := aParent
]

{ #category : 'printing' }
MicInlineElement >> print: aString surroundedOn: aStream [

	aStream 
		nextPut: $(;
		nextPutAll: aString;
		nextPut: $) 

]

{ #category : 'printing' }
MicInlineElement >> printOn: aStream [

	| contents |
	contents := children 
		ifEmpty: [ self substring ] 
		ifNotEmpty: [ self childrenPrintOn ].
	aStream nextPut: $[;
				nextPutAll: contents;
				nextPut: $];
				nextPut: $(;
				nextPutAll: self kind;
				nextPut: $) 

]

{ #category : 'printing' }
MicInlineElement >> printWithoutConventions: aStream [
	"Remove Mic and Block from the class."
	
	aStream nextPutAll: ((self class name allButFirst: 3) allButLast: 5).

]

{ #category : 'accessing' }
MicInlineElement >> start [ 	
	^ start
]

{ #category : 'accessing' }
MicInlineElement >> start: anInteger [
	start := anInteger 
]

{ #category : 'obsolete' }
MicInlineElement >> substring [
	^ substring
]

{ #category : 'obsolete' }
MicInlineElement >> substring: aSubString [
	substring := aSubString
]

{ #category : 'accessing' }
MicInlineElement >> text [
 	^ substring
]
